buildscript {
    repositories {
        maven {
            name = "Gradle Plugins"
            url = "https://plugins.gradle.org/m2/"
        }
        maven {
            name = "Minecraft Forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "com.github.jengelman.gradle.plugins:shadow:6.1.0"
    }
}
apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "maven-publish"

version = "1.1.0." + ('git rev-list --count HEAD'.execute().text.trim()) + "-1.12.2"
group = "net.buildtheearth"
archivesBaseName = "terraplusplus"

compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"

    mappings = "stable_39"
    makeObfSourceJar = false
}

configurations {
    shade
    compile.extendsFrom shade
}

repositories {
    maven {
        name = "JitPack"
        url = "https://jitpack.io/"
    }
    maven {
        name = "DaPorkchop_"
        url = "https://maven.daporkchop.net/"
    }
}

dependencies {
    //We have to use this as deobfProvided even though there's a :dev version of the artifact because otherwise mixin
    //prevents the mod from loading when running in a dev environment.
    deobfProvided ("io.github.opencubicchunks:cubicchunks:1.12.2-0.0.1282.0-SNAPSHOT") {
        transitive = false
    }

    provided ("io.github.opencubicchunks:cubicworldgen:1.12.2-0.0.206.0-SNAPSHOT:dev") {
        transitive = false
    }

    shade "com.github.DaMatrix:commons-imaging:4c6c5dfe6401a884cb4cf53fb838c99e1dfb104c"

    shade "com.fasterxml.jackson.core:jackson-databind:2.11.2"

    shade("net.daporkchop.lib:binary:0.5.7-SNAPSHOT")  {
        exclude group: "io.netty"
    }

    testCompile "junit:junit:4.12"

    compile "org.projectlombok:lombok:1.18.16"
    annotationProcessor "org.projectlombok:lombok:1.18.16"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

shadowJar {
    classifier = null
    configurations = [project.configurations.shade]

    exclude 'module-info.class'
}
build.dependsOn shadowJar

reobf { //reobfuscate the shaded JAR
    shadowJar {}
}

//relocate all shaded dependencies
task relocateShadowJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "net.buildtheearth.terraplusplus.dep"
}
shadowJar.dependsOn relocateShadowJar

publishing {
    publications {
        maven(MavenPublication) {
            groupId = "net.buildtheearth"
            artifactId = "terraplusplus"
            version = project.version

            from components.java

            artifacts.clear()
            artifact(shadowJar) {
                classifier = ""
            }
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}
