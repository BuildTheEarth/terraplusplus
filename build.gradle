allprojects {
    apply plugin: "java-library"
    apply plugin: "eclipse"
    apply plugin: "maven-publish"

    compileJava {
        sourceCompatibility = "8"
        targetCompatibility = "24"
    }

    var git_tag = 'git describe --tags --exact-match'.execute().text.trim().replace('/', '-')
    var git_branch = 'git symbolic-ref -q --short HEAD'.execute().text.trim().replace('/', '-')
    var git_commit = 'git rev-parse --short HEAD'.execute().text.trim().replace('/', '-')
    var git_version = git_tag ?: git_branch ?: git_commit

    var version_suffix = System.getenv("TMM_RELEASE") == 'true' ? "": System.getenv("TMM_VERSION_SUFFIX") ?: "-SNAPSHOT"

    version = git_version + version_suffix
    group = "net.buildtheearth.terraminusminus"

    repositories {
        mavenCentral()
        maven { // Required for leveldb
            name = "DaPorkchop_"
            url = "https://maven.daporkchop.net/"
        }
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    dependencies {
        // Annotations
        compileOnly "org.projectlombok:lombok:1.18.38"
        annotationProcessor "org.projectlombok:lombok:1.18.38"
        compileOnly 'org.jetbrains:annotations:26.0.2'

        // Tests
        testImplementation(platform('org.junit:junit-bom:5.13.4'))
        testImplementation('org.junit.jupiter:junit-jupiter')
        testRuntimeOnly('org.junit.platform:junit-platform-launcher')
    }

    publishing {
        repositories {
            maven {
                name = "smylerSnapshots"
                url = uri("https://maven.smyler.net/snapshots")
                credentials {
                    username System.getenv("MAVEN_NAME")
                    password System.getenv("MAVEN_SECRET")
                }
            }
            maven {
                name = "smylerReleases"
                url = uri("https://maven.smyler.net/releases")
                credentials {
                    username System.getenv("MAVEN_NAME")
                    password System.getenv("MAVEN_SECRET")
                }
            }
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

}


dependencies {

    // Some dependencies are inherited from the top-level allprojects Gradle directive.

    implementation "org.apache.commons:commons-imaging:1.0.0-alpha5"
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.19.2'
    implementation("net.daporkchop.lib:binary:0.5.7-SNAPSHOT")  {
        exclude group: "io.netty"
    }
    implementation "it.unimi.dsi:fastutil:8.5.16"
    implementation "com.google.code.gson:gson:2.13.1"
    implementation "com.google.guava:guava:33.4.8-jre"
    implementation "io.netty:netty-all:4.2.3.Final"
    implementation "org.apache.logging.log4j:log4j-api:2.25.1"
    implementation "org.apache.logging.log4j:log4j-core:2.25.1"

}

processResources {
    // This will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    // Replace stuff in terraminusminus.properties, nothing else
    filesMatching("**/terraminusminus.properties") {
        expand 'version': project.version
    }
}

publishing {
    publications {
        terraminusminus(MavenPublication) {
            groupId = project.group
            artifactId = "terraminusminus-core"
            version = project.version
            from(components["java"])
        }
    }
}

tasks.register('delombok') {
    description 'Delomboks the source code'
    group 'publishing'
    var buildDir = layout.buildDirectory.get()
    ext.outputDir = file("$buildDir/delombok")
    outputs.dir(outputDir)
    sourceSets.main.java.srcDirs.each {
        inputs.dir(it)
        ant.taskdef(classname: 'lombok.delombok.ant.Tasks$Delombok', classpath: configurations.compileClasspath.asPath, name: 'delombok')
        ant.mkdir(dir: outputDir)
        ant.delombok(verbose: 'true', encoding: 'UTF-8', to: outputDir, from: it)
    }
}

javadoc {
    dependsOn delombok
    source = delombok.outputDir
    failOnError = false
}
